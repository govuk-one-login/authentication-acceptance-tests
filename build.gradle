plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.25.0"
    id "org.sonarqube" version "6.0.1.5171"
}

group 'uk.gov.di'
version '1.0-SNAPSHOT'

sonar {
    properties {
        property "sonar.projectKey", "govuk-one-login_authentication-acceptance-tests"
        property "sonar.organization", "govuk-one-login"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

spotless {
    String ref = project.properties["ratchetFrom"]
    if (ref != null) {
        ref = ref.trim()
        if (ref.length() > 0) {
            ratchetFrom ref
        }
    }
    java {
        target '**/*.java'
        googleJavaFormat('1.11.0').aosp()
        importOrder '', 'javax', 'java', '\\#'
    }
    groovyGradle {
        target "**/*.gradle"
        greclipse().configFile("tools/spotless-gradle.properties")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

test {
    useJUnitPlatform()
}
